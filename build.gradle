import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.rubydung'
version = '1'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://maven.firstdark.dev/releases' }
}

dependencies {
    // LWJGL
    implementation platform('org.lwjgl:lwjgl-bom:3.4.0')
    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-glfw'
    implementation 'org.lwjgl:lwjgl-opengl'
    implementation 'org.lwjgl:lwjgl-stb'

    // Нативные библиотеки для Linux и Windows
    runtimeOnly "org.lwjgl:lwjgl::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-glfw::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-opengl::natives-linux"
    runtimeOnly "org.lwjgl:lwjgl-stb::natives-linux"

    runtimeOnly "org.lwjgl:lwjgl::natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-glfw::natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-opengl::natives-windows"
    runtimeOnly "org.lwjgl:lwjgl-stb::natives-windows"

    // Discord RPC (локальный файл) и JNA
    implementation files('libs/java-discord-rpc-2.0.1-all.jar')
    implementation 'net.java.dev.jna:jna:5.12.0'
}

// ===== VERSION GENERATION =====

def now = LocalDateTime.now()
def datePart = now.format(DateTimeFormatter.ofPattern("MMdd"))
def timePart = now.format(DateTimeFormatter.ofPattern("HHmm"))

def versionFile = file("src/main/resources/version.properties")

def versionString = ".ruby-${datePart}"

// если в этот день уже делали сборку -> добавляем HHmm
if (versionFile.exists() && versionFile.text.contains(".ruby-${datePart}")) {
    versionString = ".ruby-${datePart}-${timePart}"
}

tasks.register("generateVersionProperties") {
    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.text = "version=${versionString}\n"
        println "[Build] Version set to: ${versionString}"
    }
}

processResources.dependsOn("generateVersionProperties")
shadowJar.dependsOn("generateVersionProperties")

// ===== SHADOWJAR SETTINGS =====

shadowJar {
    manifest {
        attributes 'Main-Class': 'SwordsGame.core.Base'
    }

    archiveBaseName.set("rd-rts")
    archiveClassifier.set(versionString.replace(".ruby-", ""))
    archiveVersion.set("")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

// чтобы build всегда собирал shadowJar
build.dependsOn shadowJar
